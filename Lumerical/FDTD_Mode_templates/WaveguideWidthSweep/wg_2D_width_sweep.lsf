## By Rui Cheng 2022, HFUT, China (rcheng@hfut.edu.cn)

# perform mode calculations for variou widths of the waveguide

switchtolayout;
closeall;
clc;
clear;

# mode polarization selection 
# mode_po = 0: only TE modes; 1: only TM modes; 2: both TE and TM modes
mode_po = 2;

# set the boundary conditions to filter out the uncalculated polarization and also speed up the calculations
if (mode_po == 0) {
    setnamed('FDE',"z min bc",'symmetric');
}else if (mode_po == 1){
    setnamed('FDE',"z min bc",'anti-symmetric');
}else if (mode_po == 2){
    setnamed('FDE',"z min bc",'metal');
}

# define the waveguide widths to sweep
widths1=[0.3:0.025:0.4]*1e-6; 
widths2=[0.7:0.025:0.82]*1e-6; 
widths = [widths1;widths2];

# define the number of the modes to be included in the neff plot
modes = 5;      

wavelength = 1.55e-6; # simulation wavelength

setnamed('FDE',"number of trial modes",modes);
setnamed('FDE',"wavelength",wavelength);

neff_te = matrix (length(widths), modes );
neff_tm = neff_te;
TE_pol = matrix (length(widths), modes );
n_w = length(widths);

for(ii=1:n_w)  {
    switchtolayout;
    setnamed("waveguide","y span", widths(ii));
    
    width_margin  = 0.6e-6;
    Y_span = 2*width_margin + widths(ii);
    setnamed('FDE',"y span",Y_span);
    
    n=findmodes;
    j_te = 1;j_tm = 1;
    for (m=1:modes) {   # extract mode data
     
        TE_pol(ii,m) = getdata("FDE::data::mode"+num2str(m),"TE polarization fraction");
        if(TE_pol(ii,m) > 0.5) {
            neff_te (ii,j_te) = abs( getdata ("FDE::data::mode"+num2str(m),"neff") );
            j_te = j_te + 1;
        } else if ( TE_pol(ii,m) < 0.5 ) {
            neff_tm (ii,j_tm) = abs( getdata ("FDE::data::mode"+num2str(m),"neff") );
            j_tm = j_tm + 1;
        }
    }
}
n_te = j_te-1; n_tm = j_tm-1;
leg = cell(n_te + n_tm);
leg_index = 1;
if ((n_te != 0) & (mode_po != 1)) {
    for (i=1:n_te){
        leg{leg_index}='TE'+num2str(i-1);
        leg_index = leg_index +1;
    }
    plot (widths*1e+6, neff_te(:,(1:n_te)),'width (um)','neff','neff-versus-width',"plot type=line, marker style=o,  pen=--, linewidth=2, marker size = 10");
    holdon;
}

if ((n_tm != 0) & (mode_po != 0)){
    for (i=1:n_tm){
        leg{leg_index}='TM'+num2str(i-1);
        leg_index = leg_index +1;
    }
    plot (widths*1e+6, neff_tm(:,(1:n_tm)),'width (um)','neff','neff-versus-width',"plot type=line, marker style=o, pen=-., linewidth=2, marker size = 10");
}
leg = leg{1:leg_index-1};
legend(leg);
holdoff;

